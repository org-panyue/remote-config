//通用配置
ext {

    maven = [
         username: 'admin',
         password: 'admin123',
         repository: 'http://101.37.87.61:8085/repository/yichi/'
    ]

    app_versions = [
            versionCode: 1,
            versionName: "1.0.0"
    ]

    build_versions = [
            minSdk    : 21,
            targetSdk : 34,
            compileSdk: 34,
            java      : JavaVersion.VERSION_1_8,
            jvmTarget : '1.8'
    ]


    deps = [

            //插件依赖
            plugin: [
                    agp: "com.android.tools.build:gradle:8.2.2",
                    kgp: "org.jetbrains.kotlin:kotlin-gradle-plugin:1.9.22",
                    appLifecycleDispatchPlugin: "com.yichi.plugin:AppLifecycleDispatchPlugin:1.5.3"
            ],

            //Application 声明周期分发
            appLifeDispatch: [
                    annotation: 'com.yichi:AppLifecycleDispatch-Annotation:1.5.0',
                    processor: 'com.yichi:AppLifecycleDispatch-Processor:1.5.0',
                    api: 'com.yichi:AppLifecycleDispatch-Api:1.5.3'
            ],

            //unit test
            unitTest: [
                    junit: 'junit:junit:4.13.2',
                    junitVersion: 'androidx.test.ext:junitVersion:1.1.5',
                    espressoCore: 'androidx.test.espresso:espresso-core:3.5.1'
            ],

            //AndroidX 官方库
            androidx: [
                    appcompat: "androidx.appcompat:appcompat:1.6.1",
                    material: 'com.google.android.material:material:1.11.0',
                    multidex: 'androidx.multidex:multidex:2.0.1'
            ],

            //kotlin
            kotlin: [
                    // 扩展库
                    android_core_ktx    : "androidx.core:core-ktx:1.12.0",
            ],

            //路由
            router: [
                    api: 'com.alibaba:arouter-api:1.5.2',
                    compiler: 'com.alibaba:arouter-compiler:1.5.2'
            ],

            //yichi 自己封装编写的库
            yichi: [
                    router: 'com.yichi:module-router:1.0.0-SNAPSHOT'
            ]


    ]

    addRepos = this.&addRepos
}


//添加仓库
def addRepos(RepositoryHandler handler) {
    //Google 仓库
    handler.google()
    //Maven 仓库
    handler.mavenCentral()
    //gradle 插件仓库
    handler.gradlePluginPortal()
    //jitpack 仓库
    handler.maven {
        url 'https://jitpack.io'
    }
    //jcenter 阿里云镜像仓库
    handler.maven {
        url 'https://maven.aliyun.com/repository/jcenter'
    }
    handler.maven {
        allowInsecureProtocol true
        credentials {
            username maven.username
            password maven.password
        }
        url maven.repository
    }
}


//application 和 library 统一配置
ext {
    //设置App配置
    setAppDefaultConfig = {
        extension ->
            //Android 插件
            extension.apply plugin: 'com.android.application'
            //kotlin 插件
            extension.apply plugin: 'org.jetbrains.kotlin.android'
            //kotlin-kapt
            extension.apply plugin: 'kotlin-kapt'
            //kotlin-parcelize
            extension.apply plugin: 'kotlin-parcelize'
            //描述
            extension.description "app"
            //配置
            setAndroidConfig extension.android

            extension.configurations.all {
                resolutionStrategy {
                    //强制版本

                }
            }

            setDependencies extension.dependencies

    }

    //设置Lib配置
    setLibDefaultConfig = {
        extension ->
            //库插件
            extension.apply plugin: 'com.android.library'
            //kotlin 插件
            extension.apply plugin: 'org.jetbrains.kotlin.android'
            //kotlin-kapt
            extension.apply plugin: 'kotlin-kapt'
            //kotlin-parcelize
            extension.apply plugin: 'kotlin-parcelize'
            //描述
            extension.description "lib"

            setAndroidConfig extension.android


            extension.configurations.all {
                resolutionStrategy {


                }

            }

            setDependencies extension.dependencies
    }


    //设置Android配置
    setAndroidConfig = {
        extension ->
            extension.compileSdk build_versions.compileSdk
            extension.defaultConfig {
                minSdk build_versions.minSdk
                targetSdk build_versions.targetSdk
                versionCode app_versions.versionCode
                versionName app_versions.versionName

                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

                javaCompileOptions {
                    annotationProcessorOptions {
                        arguments = [AROUTER_MODULE_NAME: project.getName()]
                        println 'AROUTER_MODULE_NAME：' + project.getName()
                    }
                }
            }


            extension.sourceSets {
                main {
                    jniLibs.srcDirs = ['libs']
                    java.srcDirs = ['src/main/java']
                    res.srcDirs = ['src/main/res']
                }
            }

            extension.compileOptions {
                sourceCompatibility build_versions.java
                targetCompatibility build_versions.java
            }

            extension.kotlinOptions {
                jvmTarget = build_versions.jvmTarget
            }

            extension.configurations {
                all*.exclude group: 'com.android.support', module: 'support-compat'
                all*.exclude group: 'com.android.support', module: 'versionedparcelable'
            }

    }

    //设置依赖
    setDependencies = {
        extension ->
            extension.implementation fileTree(dir: 'libs', include: ['*.jar'])

            extension.implementation deps.kotlin.android_core_ktx
            extension.implementation deps.androidx.appcompat
            extension.implementation deps.androidx.material

            //路由
            extension.implementation deps.yichi.router
            extension.kapt deps.router.compiler

            //unit test
//            extension.implementation deps.unitTest.junit
//            extension.androidTestImplementation deps.unitTest.junitVersion
//            extension.androidTestImplementation deps.unitTest.espressoCore
    }
}

